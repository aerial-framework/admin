<?xml version="1.0" encoding="utf-8"?>
<mx:HBox xmlns:mx="http://www.adobe.com/2006/mxml" verticalAlign="middle" enabled="{editable}"
		 creationComplete="init()" implements="components.config.IConfigItem">
	<mx:Metadata>
		[Event(name="change",type="flash.events.Event")]
	</mx:Metadata>
	
	<mx:CheckBox id="execute" labelPlacement="bottom" label="Execute" change="dispatchEvent(event)"/>
	<mx:CheckBox id="write" labelPlacement="bottom" label="Write" change="dispatchEvent(event)"/>
	<mx:CheckBox id="read" labelPlacement="bottom" label="Read" change="dispatchEvent(event)"/>
	
	<mx:Script>
		<![CDATA[
			import controllers.ApplicationController;
			
			import utils.ConfigurationUtil;
			import utils.PermissionsUtil;
			private var _mode:String;
			
			private function init():void
			{
				ApplicationController.instance.configViewChanged.add(viewChangeHandler);
			}
			
			private function viewChangeHandler(mode:String, enabled:Boolean):void
			{				
				this.editable = enabled;
			}
			
			public function set mode(value:String):void
			{
				_mode = value;
				var permissions:Array = PermissionsUtil.parseMode(uint(value));
				
				execute.selected = write.selected = read.selected = false;
				
				for each(var permission:String in permissions)
				{
					if(permission == PermissionsUtil.EXECUTE)
						execute.selected = true;
					
					if(permission == PermissionsUtil.WRITE)
						write.selected = true;
					
					if(permission == PermissionsUtil.READ)
						read.selected = true;
				}
				
				dispatchEvent(new Event(Event.CHANGE));
			}
			
			[Bindable(event="change")]
			public function get mode():String
			{
				var permissions:Array = [PermissionsUtil.NONE];
				
				if(execute.selected || write.selected || read.selected)
					permissions = [];
				
				if(execute.selected)
					permissions.push(PermissionsUtil.EXECUTE);
				
				if(write.selected)
					permissions.push(PermissionsUtil.WRITE);
				
				if(read.selected)
					 permissions.push(PermissionsUtil.READ);
				
				return PermissionsUtil.parsePermission(permissions);
			}
			
			public function getValue():Object
			{
				return mode;
			}
		]]>
	</mx:Script>
	
	<mx:Boolean id="editable">true</mx:Boolean>
</mx:HBox>